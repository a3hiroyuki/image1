Option Explicit
 
Public Sub InsertImages2()
'指定したフォルダ内の画像ファイルを一括挿入
  Dim prs As PowerPoint.Presentation
  Dim sld As PowerPoint.Slide
  Dim shp As PowerPoint.Shape
  Dim tmp As PowerPoint.PpViewType
  Dim fol As Object, f As Object
  Dim fol_path As String
  Dim f1 As Object
  Dim f2 As Object
   
  Set prs = ActivePresentation
   
  'スライドショー表示になっていたら解除
  If SlideShowWindows.Count > 0 Then prs.SlideShowWindow.View.Exit
   
  With ActiveWindow
    tmp = .ViewType 'ウィンドウの表示モード記憶
    .ViewType = ppViewSlide
  End With
   
  '画像フォルダ取得
  Set fol = CreateObject("Shell.Application") _
            .BrowseForFolder(0, "画像フォルダ選択", &H10, 0)
  If fol Is Nothing Then GoTo Fin
  fol_path = fol.Self.Path
   
  'フォルダ内のファイル処理
  With CreateObject("Scripting.FileSystemObject")
    If Not .FolderExists(fol_path) Then GoTo Fin
    For Each f1 In .GetFolder(fol_path).SubFolders
    ' MsgBox f1.Path
        For Each f2 In .GetFolder(f1.Path).Files
        ' MsgBox f2.Path
           'JPEGファイルのみ処理
           Select Case LCase(.GetExtensionName(f2.Path))
             Case "jpg", "jpeg", "png"
               Set sld = prs.Slides.Add(prs.Slides.Count + 1, ppLayoutBlank)
               sld.Select
               Set shp = sld.Shapes.AddPicture(FileName:=f2.Path, _
                                               LinkToFile:=False, _
                                               SaveWithDocument:=True, _
                                               Left:=0, _
                                               Top:=0)
               With shp
                 .LockAspectRatio = True '縦横比を固定
                  
                 '挿入した画像をスライドのサイズに合わせる
                 If .Width > .Height Then
                   .Width = prs.PageSetup.SlideWidth
                 Else
                   .Height = prs.PageSetup.SlideHeight
                 End If
                  
                 .Select
               End With
                
               '画像をスライド中央に配置
               With ActiveWindow.Selection.ShapeRange
                 .Align msoAlignCenters, True
                 .Align msoAlignMiddles, True
               End With
           End Select
         Next
    Next
  End With
Fin:
  ActiveWindow.ViewType = tmp 'ウィンドウの表示モードを元に戻す
End Sub
